plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.sonarqube' version '5.1.0.4882'
}

group = 'com.jreinhal'
version = '2.2.0'

// Edition selection via -Pedition=xxx (default: enterprise)
def edition = project.hasProperty('edition') ? project.property('edition') : 'enterprise'
ext.edition = edition

// Log the edition being built
println ">>> Building SENTINEL edition: ${edition} <<<"

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

def enableLint = project.hasProperty('lint') || (System.getenv('CI') != null && !System.getenv('CI').isBlank())
if (enableLint) {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
        if (project.hasProperty('lintWerror')) {
            options.compilerArgs += ['-Werror']
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // DevTools for hot-reload during development (disabled in production)
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Spring AI for Ollama (Local)
    implementation 'org.springframework.ai:spring-ai-ollama-spring-boot-starter:1.0.0-M4'
    
    // Spring AI for Cloud Models (Foundation)
    implementation 'org.springframework.ai:spring-ai-openai-spring-boot-starter:1.0.0-M4'
    implementation 'org.springframework.ai:spring-ai-anthropic-spring-boot-starter:1.0.0-M4'
    implementation 'org.springframework.ai:spring-ai-vertex-ai-gemini-spring-boot-starter:1.0.0-M4'

    implementation 'org.springframework.ai:spring-ai-mongodb-atlas-store-spring-boot-starter:1.0.0-M4'
    implementation 'org.springframework.ai:spring-ai-pdf-document-reader:1.0.0-M4'

    // Apache Tika for file type detection
    implementation 'org.apache.tika:tika-core:2.9.1'
    implementation 'org.apache.tika:tika-parsers-standard-package:2.9.1'

    // Caffeine cache
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    // Rate limiting
    implementation 'com.bucket4j:bucket4j-core:8.7.0'

    // OpenAPI/Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // JWT validation for OIDC
    implementation 'com.nimbusds:nimbus-jose-jwt:9.37.3'

    // AWS KMS (optional key management for HIPAA deployments)
    implementation 'software.amazon.awssdk:kms:2.25.62'
    // AWS S3 connector (optional ingestion)
    implementation 'software.amazon.awssdk:s3:2.25.62'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.ai:spring-ai-bom:1.0.0-M4'
    }
}

// SCIF/air-gapped builds benefit from reproducible dependency resolution.
dependencyLocking {
    lockAllConfigurations()
}

// Edition-specific source sets with actual exclusion logic
def editionExcludes = { ->
    if (!(edition in ['trial', 'enterprise', 'medical', 'government'])) {
        exclude '**/enterprise/**'
        exclude '**/medical/**'
        exclude '**/government/**'
    } else if (edition in ['trial', 'enterprise']) {
        exclude '**/medical/**'
        exclude '**/government/**'
    } else if (edition == 'medical') {
        exclude '**/government/**'
    }
    // Government edition includes all packages (no exclusions)
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
            editionExcludes.delegate = delegate
            editionExcludes()
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
            editionExcludes.delegate = delegate
            editionExcludes()
        }
    }
}

tasks.named('bootRun') {
    jvmArgs = ['--add-opens', 'java.base/java.nio.charset=ALL-UNNAMED']
}

def docsIndexSource = file('docs/README.md')
def docsIndexTarget = file('src/main/resources/static/docs-index.md')

tasks.register('syncDocsIndex') {
    description = 'Sync docs/README.md into static docs-index.md for the UI.'
    inputs.file(docsIndexSource)
    outputs.file(docsIndexTarget)
    doLast {
        if (!docsIndexSource.exists()) {
            throw new GradleException("Missing ${docsIndexSource}")
        }
        docsIndexTarget.parentFile.mkdirs()
        docsIndexTarget.text = docsIndexSource.text
    }
}

processResources {
    dependsOn tasks.named('syncDocsIndex')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Edition-aware JAR naming
bootJar {
    archiveBaseName = "sentinel-${edition}"
    archiveVersion = version
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

sonar {
    properties {
        property 'sonar.projectKey', 'jreinhal_mercenary-project'
        property 'sonar.organization', 'jreinhal'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property 'sonar.exclusions', 'src/main/resources/static/vendor/**,src/main/resources/static/js/sentinel-app.js'
        property 'sonar.cpd.exclusions', 'src/main/resources/static/vendor/**,src/main/resources/static/js/sentinel-app.js'
    }
}

tasks.register('ciE2eTest', Test) {
    description = 'Run CI-lite pipeline E2E tests.'
    group = 'verification'
    useJUnitPlatform()
    include '**/PipelineE2eTest.class'
}
